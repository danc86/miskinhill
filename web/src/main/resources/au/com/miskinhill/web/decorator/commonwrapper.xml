<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns="http://www.w3.org/1999/xhtml"
                xmlns:html="http://www.w3.org/1999/xhtml"
                xsl:version="1.0"> 
<xsl:output method="xml" doctype-system="http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd" doctype-public="-//W3C//DTD XHTML 1.0 Strict//EN" /> 

<!-- identity -->
<xsl:template match="@*|node()">
    <xsl:copy>
        <xsl:apply-templates select="@*|node()" />
    </xsl:copy>
</xsl:template>

<xsl:template match="html:div[@class='incompleteness-warning']">
    <div class="incompleteness-warning" lang="en">
        <p>The full HTML version of this article is not currently available online.
        Please refer to the <a><xsl:attribute name="href"><xsl:value-of select="//html:a[@rel='alternate' and @type='application/pdf']/@href" /></xsl:attribute>original print version</a>.</p>
    </div>
</xsl:template>

<xsl:template match="html:html">
    <html lang="en"><xsl:apply-templates /></html>
</xsl:template>

<xsl:template match="html:meta[not(@content)]">
    <meta>
        <xsl:attribute name="content">
            <xsl:value-of select="./text()" />
        </xsl:attribute>
        <xsl:for-each select="@*">
            <xsl:attribute name="{name(.)}">
                <xsl:value-of select="." />
            </xsl:attribute>
        </xsl:for-each>
    </meta>
</xsl:template>

<xsl:template match="html:head">
    <head>
        <xsl:for-each select="@*">
            <xsl:attribute name="{name(.)}">
                <xsl:value-of select="." />
            </xsl:attribute>
        </xsl:for-each>
        <title><xsl:value-of select="html:title" /> - Miskin Hill</title>
        <meta http-equiv="content-type" content="text/html; charset=utf-8" />
        <link rel="unapi-server" type="application/xml" title="unAPI" href="/unapi" />
        <link rel="stylesheet" type="text/css" href="/style/common.css" />
        <xsl:apply-templates select="*[name() != 'title']" />
        <script type="text/javascript">
            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-12556962-1']);
            _gaq.push(['_trackPageview']);
            (function() {
              var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
              ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
              var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();
        </script>
    </head>
</xsl:template>

<xsl:template match="html:body">
    <body>
        <xsl:for-each select="@*">
            <xsl:attribute name="{name(.)}">
                <xsl:value-of select="." />
            </xsl:attribute>
        </xsl:for-each>
        <div id="banner">
            <h1><a href="/">Miskin Hill</a></h1>
            <ul>
                <li><a href="/journals/">journals</a></li>
                <li><a href="/about/">about</a></li>
                <li><a href="/contact/">contact</a></li>
            </ul>
        </div>
        <div id="bodywrapper">
            <div id="feedback"><a href="/contact/" title="Report a problem or make a suggestion about this page">Feedback</a></div>
            <xsl:apply-templates select="*" />
            <div id="footer">
                Hosted by <a href="http://www.sjkwi.com.au/">SJK Web Industries</a>.
                Content copyright the publishers and each contributor.
                <a href="http://code.miskinhill.com.au/">Code</a> copyright Miskin Hill Academic Publishing.
                Metadata is free!
            </div>
        </div>
    </body>
</xsl:template>

</xsl:stylesheet>
